---
title: "Applied Category Theory with Scryer Prolog and CLP(ℤ): a case study in dose-escalation"
subtitle: "Scryer Prolog Meetup 2024 --- Hotel Stefanie, Vienna"
author: "David C. Norris"
filters:
  - diagram
format:
  revealjs:
    embed-resources: true
    auto-stretch: false
diagram:
  engine:
    tikz:
      execpath: pdflatex
      header-includes:
        - '\usepackage[mathscr]{eucal}'
        - '\usepackage{amsmath}'
        - '\usepackage{amssymb}'
        - '\usepackage{mathtools}'
        - '\usepackage{tikz}'
        - '\usepackage{tikz-cd}'
        - '\usetikzlibrary{shapes}'
        - '\newcommand{\D}{\mathcal{D}}'
        - '\newcommand{\Q}{\mathcal{Q}}'
code-annotations: below
---

## An update

::: hidden
\newcommand{\N}{\mathbb{N}}
\newcommand{\R}{\mathbb{R}}
\renewcommand{\S}{\mathcal{S}}
\newcommand{\A}{\mathcal{A}}
\newcommand{\C}{\mathcal{C}}
\newcommand{\D}{\mathcal{D}}
\newcommand{\Q}{\mathcal{Q}}
\newcommand{\M}{\mathcal{M}}
\newcommand{\IE}{\Q \xrightarrow{\;\;E\;\;} \D}
\newcommand{\E}{\widehat{E}}
\newcommand{\delihat}{\surd\E}
:::

![Norris & Triska (2024)](Executable-Spec-arXiv-2024.png)

# Orientation to oncology dose-finding

Inertial forces of history

## Up-and-down designs in explosives research

![Example of data from an up-and-down design in explosives research.  Source: Dixon & Mood (1948) "A Method for Obtaining and Analyzing Sensitivity Data" *J. Am. Stat. Assoc.*](Dixon-Mood-Fig1.png)

## 'Sensitivity data' are conceived as inherently *censored*

![](Dixon-Mood-sensitivity-def.png)

## Ethical assumptions

* Typically, dose-finding trials are 'first-in-humans' (FIH) trials

* Oncology drugs are too toxic for healthy-volunteer studies

* So FIH oncology trials are pursued with *therapeutic intent*,
  in patients who have exhausted all standard treatments
  
* Nevertheless, *scientifically* the emphasis in dose-finding trials
  is primarily on toxicity, rather than efficacy

* Thus, participants are "resources" [*sic* --- as recent as 2023!]
  used for learning about drug toxicities

## Statistical assumptions

* Occurrence of a binary 'dose-limiting toxicity' (DLT) is recorded
  after a 'first cycle' of treatment, typically 4 weeks

* A DLT is akin to the explosions of Dixon & Mood

* Each trial participant yields one Bernoulli trial, DLT=0|1

* These are *exchangeable* (participants lack individuality)

* Trial concludes with a 'recommended phase 2 dose' (RP2D)

::: aside
The DTAT research programme rejects many of the foregoing
assumptions, aiming to enable clinical *dose individualization*.
:::

## Pharmacologic assumptions

> Alle Dinge sind Gift, und nichts ist ohne Gift;
> allein die Dosis macht, dass ein Ding kein Gift ist.
> 
> --- Paracelsus, 1538

* Strictly monotone dose-response
* Higher doses are both more efficacious and more toxic


# The '3 + 3' design

The Original Sin of oncology dose finding

## 3 + 3 by example

```{.verbatim code-line-numbers="1-4|6-9|11-14|"}
Level 3                        x x o  |  2/3  <-- Too toxic 
Level 2          o x o  o o o         |  1/6  <-- MTD = RP2D
Level 1   o o o                       |  0/3
Cohort#     1      2      3      4    | Tally 
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Level 3                 x x o         |  2/3  <-- Too toxic 
Level 2          o o o         o x o  |  1/6  <-- MTD = RP2D
Level 1   o o o                       |  0/3
Cohort#     1      2      3      4    | Tally
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Level 3                        |
Level 2          x o x         |  2/3  <-- Too toxic 
Level 1   o o o         o o o  |  0/6  <-- MTD = RP2D
Cohort#     1      2      3    | Tally
```

::: {.incremental}
- 'Markov property'
- Synchronous cohorts
:::

## 'Markov property'

![The 'Markov' or 'lack-of-memory' property of 3+3 method as described by Clertant & O'Quigley (2017) "Semiparametric dose finding methods" *J. R. Statist. Soc. B*](Clertant+O'Quigley-2017.png)


## Synchronously dosed cohorts
::: {layout-ncol=2}

![Callaway & Butler (2016) "Researchers question design of fatal French clinical trial" *Nature*](Callaway+Butler-2016-Nature.png){.lightbox}

![Wadman (2006) "London's disastrous drug trial has serious side effects for research" *Nature*](TeGenero.png){.lightbox}
:::

---

:::: {.columns}

::: {.column width="70%"}
![](Skolnik08-Table1.png){.lightbox}
:::

::: {.column width="30%"}
Note the fine print!
:::

::::


::: aside
Skolnik JM, Barrett JS, Jayaraman B, Patel D, Adamson PC. Shortening the timeline of pediatric phase I trials: the rolling six design. *J Clin Oncol.* 2008;26(2):190-195. doi:10.1200/JCO.2007.12.7712
:::

---

:::: {.columns}

::: {.column width="50%"}
![](Frankel20-Table1.png){.lightbox}
:::

::: {.column width="50%"}
Frankel PH, Chung V, Tuscano J, et al. Model of a Queuing Approach for Patient Accrual in Phase 1 Oncology Studies. *JAMA Network Open.* 2020;3(5):e204787-e204787. doi:10.1001/jamanetworkopen.2020.4787

Drowning in a combinatorial explosion of fine print?

:::

::::


## 93 final tallies of the 3-dose 3+3 {.scrollable}

![](hasse3.png){width=80% .lightbox}

## 29 final tallies of the 2-dose 3+3 {.scrollable}

![](hasse2.svg){width=100%}

## Metagraphs

- *Objects* $a, b, c, ...$
- *Arrows* $f, g, h, ...$
- Operations *Domain* and *Codomain* assigning objects $a = \mathrm{dom} f$ and $b = \mathrm{cod} f$ to each arrow $f$,
  a situation we can depict as
  $$
  a \xrightarrow{f} b.
  $$

::: aside
Saunders Mac Lane. 1998. *Categories for the Working Mathematician.* 2ed.
:::

## Metacategories have in addition ..

- *Identity* operation $\mathrm{id}$ giving an arrow $a \xrightarrow{\mathrm{id}_a} a$ for each $a$
- *Composition* giving for each pair $\langle f, g \rangle$ with $\mathrm{dom}f = \mathrm{cod}g$ an $f \circ g$ such that
``` {.tikz}
%%| width: 100%
%%| height: 40%
\begin{tikzcd}
    a \ar[rrr, "k\circ(g\circ f) = (k\circ g)\circ f"]
    \ar[ddrrr, near end, "g\circ f"{description}]
    \ar[dd, "f"']
    & & & d
    \\ \\
    b \ar[rrr, "g"']
    \ar[uurrr, near end, "k\circ g"{description}]
    & & & c \ar[uu, "k"']
\end{tikzcd}\qquad\text{and}\qquad
\begin{tikzcd}
    a \ar[ddrr, "f"'] \ar[rr, "f"] & & b \ar[dd, "1_b"] \ar[ddrr, "g"]
    \\ \\
    & & b \ar[rr, "g"'] & & c
\end{tikzcd}
```

::: aside
Saunders Mac Lane. 1998. *Categories for the Working Mathematician*, 2ed. pp.7--8
:::


## Category

An interpretation of the preceding axioms within set theory.

![The category **3**](IMG_0328.JPG){width=80%}

## Concrete Categories

* **Set:** sets are the objects, and functions are the arrows

* **Rel:** sets and binary relations

* **Top:** topological spaces and continuous functions

* **Group:** groups and group homomorphisms

* **Poset:** partially ordered sets and monotone maps

* **Cat:** categories and arrow-respecting maps called *functors*

## Abstract Categories

* **Monoid:** a category with 1 object
  - Same as a *semigroup with unit*
  - A monoid with inverses is a *group*

* **Preorder:** for any pair $\langle a, b \rangle$, there is *at most* 1 arrow $a \rightarrow b$
  - The arrows $a \rightarrow b$ are often written $a \le b$
  - This definition allows both $a \le b\;$ and $\;b \le a$
  - In a *partial order*, $a \le b \wedge b \le a \Rightarrow a = b$
  - Identity arrows $\Rightarrow$ reflexivity; composition $\Rightarrow$ transitivity

## Dosewise Tallies

```prolog
:- use_module(library(clpz)).
clpz:monotonic.

q_r(T/N, T:U) :- 0 #=< #T, 0 #=< #U, #N #= #T + #U.
```

Denote the set of *dosewise tallies* by
$$
Q = \{t/n \mid t, n \in \N, t \le n\}
$$
or in 'ratio form'
$$
R = \{t\!:\!u \mid t, u \in \N\},
$$
with $u = n - t$ denoting counts of non-toxicities.


## Q is a commutative monoid

$$
\frac{t_1}{n_1} + \frac{t_2}{n_2} = \frac{t_1+t_2}{n_1+n_2},
$$

Observe that $\frac{0}{0} \in Q$ is the unit.

$$
(Q,0/0,+) \cong (\N,0,+)\times(\N,0,+)
$$

## An *evident safety* relation on Q

Let $\preceq$ be the transitive closure of a preorder relation satisfying,
\begin{equation}
    \frac{t}{n}\!+\!\frac{1}{1} \;\preceq\; \frac{t}{n} \;\preceq\; \frac{t}{n}\!+\!\frac{0}{1} \quad \forall\; \frac{t}{n} \in Q.
\end{equation}
    Then the preorder $(Q,\preceq)$ compares the **evident safety** expressed in dosewise tallies, such that we read
$$
q_1 \preceq q_2
$$
as "$q_1$ is evidently no safer than $q_2$" or "$q_2$ is evidently at least as safe as $q_1$".

---

### $(Q,\preceq,\frac{0}{0},+)$ is a monoidal preorder

In particular, we have the *monotonicity condition*,
$$
q \preceq q',\; g \preceq g' \implies q + g \preceq q' + g',
$$
arising by induction from the our definition of $\preceq$ in terms of $+$.

* Indeed, it is a *symmetric* monoidal preorder, since $+$ is commutative!

## Characterization of $\preceq$ on $R$

$$
t\!:\!u \preceq t'\!:\!u' \iff t \ge t' \;\text{and}\; u \le u'
$$

This follows from the ratio formulation of $\preceq$,
$$
    t\!:\!u + 1\!:\!0 \preceq t\!:\!u \preceq t\!:\!u + 0\!:\!1 \quad \forall\; t\!:\!u \in R.
$$

In $Q$, this translates to,
$$
\frac{t}{n} \preceq \frac{t'}{n'} \iff t \ge t' + \max(0,n-n').
$$

## Extending $\preceq$ to [full] tallies in $Q^D$

* WLOG, consider $D=2$

* Pharmacologic monotonicity engenders *logical implications*
  - $\left(\frac{1}{1},\frac{0}{0}\right) \implies \left(\frac{0}{0},\frac{1}{1}\right) \quad \text{or} \quad \left(\frac{1}{1},\frac{0}{0}\right) \supseteq \left(\frac{0}{0},\frac{1}{1}\right)$
  - $\left(\frac{0}{1},\frac{0}{0}\right) \impliedby \left(\frac{0}{0},\frac{0}{1}\right) \quad \text{or} \quad \left(\frac{0}{1},\frac{0}{0}\right) \subseteq \left(\frac{0}{0},\frac{0}{1}\right)$

* Since toxicities are bad and non-toxicities good, we have
  - $\left(\frac{1}{1},\frac{0}{0}\right) \preceq \left(\frac{0}{0},\frac{1}{1}\right)$ (RHS has less evidence of a *bad* thing)
  - $\left(\frac{0}{1},\frac{0}{0}\right) \preceq \left(\frac{0}{0},\frac{0}{1}\right)$ (LHS has less evidence of a *good* thing)

## Embracing 'arrows' qua 'narrative'

One talks of "applying" a function to an argument, of a function
"acting" on a domain.  There is a definite impression of action,
even of motion, as evidenced by the use of the arrow symbol, the
source-target terminology, and commonly used synonyms for "function"
like "transformation" and "mapping".  ...  **This dynamical quality
that we have been describing is an essential part of the meaning of the
word "function" as it is used in mathematics.**  The "ordered-pairs"
definition does not convey this.

::: aside
Robert Goldblatt. *Topoi: The Categorial Analysis of Logic.* Dover edition, 2006.
:::

## Breaking the x|o symmetry

* Claim: $\left(\frac{1}{1},\frac{0}{1}\right) \preceq \left(\frac{0}{1},\frac{1}{1}\right)$

```{.verbatim code-line-numbers="1-4|6-9|"}
Pt ID     A B C  D E F
Level 2          x o x |  2/3
Level 1   o o o        |  0/3
Cohort#     1      2   | Tally = [0/3,2/3] 
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Pt ID     A B D  C E F
Level 2          o o x |  1/3
Level 1   o o x        |  1/3
Cohort#     1      2   | Tally = [1/3,1/3] 
```

::: {.incremental}
* $\left(\frac{1}{3},\frac{1}{3}\right) \preceq \left(\frac{0}{3},\frac{2}{3}\right)$
* $q + \left(\frac{1}{1},\frac{0}{1}\right) \preceq_\text{exch} \left(\frac{0}{1},\frac{1}{1}\right) + q$, where $q = \left(\frac{0}{2},\frac{1}{2}\right) \in Q^2$
:::

## Notation

For $q, q' \in Q$ and $j, k \in \{1,...,D\}$ we write:

* $\langle q \rangle_j := (\frac{0}{0},\dots,\frac{0}{0},q,\frac{0}{0},...,\frac{0}{0}) \in Q^D, \;q$ in $j$'th position

* $\langle q, q' \rangle_{j,k} := \langle q \rangle_j + \langle q' \rangle_k,$ with $j < k$ understood.

---

### Arrows of the *monoidal* preorder $(Q^D,\preceq)$

\begin{align*}
q + \langle\frac{1}{1},\frac{0}{1}\rangle_{j,k} &\preceq_{exch} \langle\frac{0}{1},\frac{1}{1}\rangle_{j,k} + q \quad \forall q \in Q^2, j<k \in \{1,...,D\}
\\
q + \langle\frac{1}{1}\rangle_j &\preceq_{-tox} \langle\frac{0}{0}\rangle_j + q
\\
q +  \langle\frac{0}{0}\rangle_j &\preceq_{tol+} \langle\frac{0}{1}\rangle_j + q
\\
q +  \langle\frac{0}{1}\rangle_j &\preceq_{titr} \langle\frac{0}{1}\rangle_{j+1} + q
\end{align*}

Transitive closure of $\preceq_{exch} \cup \preceq_{-tox} \cup \preceq_{tol+} \cup \preceq_{titr}$ is $\preceq$.

---

## More parsimony is possible

``` {.tikz}
%%| width: 100%
%%| height: 40%
\begin{tikzcd}
    (\frac{1}{1},\frac{0}{1}) \ar[r, "\preceq_\text{exch}"]
    \ar[rrr,bend right,"\preceq_{\text{-tox}_1}"']
    & (\frac{0}{1},\frac{1}{1}) \ar[r, "\preceq_\text{titr}"]
    & (\frac{0}{0},\frac{1}{2}) \ar[r, "\preceq_{\text{-tox}_2}"]
    & (\frac{0}{0},\frac{0}{1})
\end{tikzcd}
```

---

### Monoidal 'borrowing'?

\begin{align*}
\left(\frac{1}{1},\frac{0}{1}\right) &\preceq_{exch}\left(\frac{0}{1},\frac{1}{1}\right)
\\
+ \left(\frac{0}{1},\frac{0}{0}\right) &\preceq_{titr}\;\left(\frac{0}{0},\frac{0}{1}\right)
\\
\hline \\
\left(\frac{1}{2},\frac{0}{1}\right) &\;\;\preceq\;\; \left(\frac{0}{1},\frac{1}{2}\right)
\\
\left(\frac{0}{1},\frac{0}{1}\right) + \left(\frac{1}{1},\frac{0}{0}\right) & \;\;\preceq\;\; \left(\frac{0}{0},\frac{1}{1}\right) + \left(\frac{0}{1},\frac{0}{1}\right)
\end{align*}

---

> Make no mistake about it: Computers process numbers --- not symbols.
> We measure our understanding (and control) by the extent to which
> we can arithmetize an activity.
> 
> --- Alan J. Perlis (Epigram #65)

::: {.incremental}
* Not so much anti-symbolic-computing ..
* .. as pro-CLP(ℤ)!
:::

## $\preceq$ via CLP($\mathbb{Z}$)

Every arrow of the preorder $\preceq$ may be obtained in the form,
\begin{align*}
\sum_{i=1}^D \lambda_i\langle\frac{1}{1}\rangle_i + &\sum_{j<k}\left(\eta_{jk}\langle\frac{1}{1},\frac{0}{1}\rangle_{j,k} + \theta_{jk}\langle\frac{0}{1}\rangle_j\right) +q \\
\preceq q + &\sum_{j<k}\left(\eta_{jk}\langle\frac{0}{1},\frac{1}{1}\rangle_{j,k}  + \theta_{jk}\langle\frac{0}{1}\rangle_k\right) + \sum_{i=1}^D \gamma_i\langle\frac{0}{1}\rangle_i, \label{clpz}
\end{align*}
for some $q \in Q^D$ and $\lambda_i, \eta_{jk}, \theta_{jk}, \gamma_i \in \N$.

NB: We used the commutativity of $+$ to collect terms.

## Cumulative distributions

Given a dose-indexed tuple $x = (x_d) \in \N^D$, let capitalization denote partial summation from the left or right by,
$$
X_d = \sum_{j=1}^d x_j \quad \text{and} \quad \bar{X}_d = \sum_{j=d}^D x_j
$$
and write $X = (X_d)$ or $\bar{X} = (\bar{X}_d) \in \N^D$.

Note that $X$ is an ascending sequence, and $\bar{X}$ descending.

---

### Basic machinery in $\N^D$

```prolog
intlist_partsums([X|Xs], [X|Σs]) :-
    same_length(Xs, Σs), % eliminate unnecessary choice point
    intlist_partsums_acc(Xs, Σs, X).

intlist_partsums_acc([], [], _).
intlist_partsums_acc([X|Xs], [Σ|Σs], A) :-
    #Σ #= #X + #A,
    intlist_partsums_acc(Xs, Σs, Σ).

:- op(900, xfx, '≤').
'≤'([], [], true). % trivial case makes general clause easier to implement
'≤'([X|Xs], [Y|Ys], Truth) :- % ≤ extended to ℕᴰ, D≥1
    if_(clpz_t(#X #=< #Y),
        '≤'(Xs,Ys,Truth),
        Truth = false).
    
%?- '≤'([], [], Truth).
%@    Truth = true. % A quirk easily excluded from ('≤')/2

Xs '≤' Ys :- same_length(Xs,Ys), length(Xs,D), D #> 0, '≤'(Xs,Ys,true).

%?- [] '≤' [].
%@    false. % As desired
```

---

### Invariants of the atomic arrows

\begin{align*}
q + \langle\frac{1}{1},\frac{0}{1}\rangle_{j,k} &\preceq_{exch} \langle\frac{0}{1},\frac{1}{1}\rangle_{j,k} + q \quad
& T\downarrow \quad \bar{U}\downarrow
\\
q + \langle\frac{1}{1}\rangle_j &\preceq_{-tox} \langle\frac{0}{0}\rangle_j + q
& T\downarrow \quad \bar{U} -
\\
q +  \langle\frac{0}{0}\rangle_j &\preceq_{tol+} \langle\frac{0}{1}\rangle_j + q
& T - \quad \bar{U}\uparrow
\\
q +  \langle\frac{0}{1}\rangle_j &\preceq_{titr} \langle\frac{0}{1}\rangle_{j+1} + q
& T - \quad \bar{U}\uparrow
\end{align*}

But for $\preceq_\text{exch}$, we'd have $\,q \preceq q' \iff T \ge T' \wedge \bar{U} \le \bar{U'}$.

---

### Exchange-adjustment with $T$'s and $\bar{U}$'s

```prolog
qs_Ts_Ūs(Qs, ΣTs, ΣŪs) :-
    maplist(\Q^T^U^(q_r(Q, T:U)), Qs, Ts, Us),
    intlist_partsums(Ts, ΣTs),
    reverse(Us, Ūs),
    intlist_partsums(Ūs, RΣŪs),
    reverse(RΣŪs, ΣŪs).

%?- Qs = [1/6,2/6], maplist(q_r, Qs, Rs), qs_Ts_Ūs(Qs, Ts, Ūs).
%@    Qs = [1/6,2/6], Rs = [1:5,2:4], Ts = [1,3], Ūs = [9,4].

as_Ts_Tas(As, Ts, Tas) :-
    Ts = [_|Ts1], same_length(Ts1, As),
    append(As, [0], As0), % Last of Ts is the total count of x's,
    same_length(Ts, Tas), % which is invariant under x-o exchange.
    maplist(\T^A^Ta^(#T - #A #= #Ta), Ts, As0, Tas).

%?- as_Ts_Tas(As, [1,2,3], [0,3,3]).
%@    As = [1,-1].

%?- as_Ts_Tas([1,0], [1,2,3], Ts1), as_Ts_Tas([0,-1], Ts1, Ts2).
%@    Ts1 = [0,2,3], Ts2 = [0,3,3].
```

---

### Implementing $\preceq$ via exchange-adjustment

```prolog
'≼'(Q1s, Q2s, Truth) :-
    qs_Ts_Ūs(Q1s, T1s, Ū1s),
    qs_Ts_Ūs(Q2s, T2s, Ū2s),
    Ū1s = [Ū1|Ū1rs], Ū2s = [Ū2|Ū2rs],
    % We now calculate the _smallest_ exchange-adjustment As:Ū1s⟼Ū1as
    % that would ensure Ū1as ≤ Ū2s.  (If this inequality already holds
    % for unadjusted Ū1s, then this will be the _null_ adjustment.)
    same_length(Ū1rs, As),
    maplist(\A^U1^U2^(#A #= max(0, #U1 - #U2)), As, Ū1rs, Ū2rs),
    as_Ts_Tas(As, T1s, T1as), % T1as is the post-exchange vector
    if_((clpz_t(#Ū1 #=< #Ū2), % Q1 must not have more total o's  # <1>
         T2s '≤' T1as % Post-exchange T1as must still exceed T2s
        ),
        Truth = true,
        Truth = false
       ).
```
1. $q \preceq_{exch} q^* \preceq_{\neg exch} q' \iff T^* \ge T' \wedge \bar{U}^* \le \bar{U'}$.

## Expanding expressive freedom

```prolog
'≺'(Q1s, Q2s, Truth) :-
    if_((Q1s '≼' Q2s, dif(Q1s, Q2s)),  # <1>
        Truth = true,
        Truth = false).

'≽'(Q2s, Q1s, Truth) :-'≼'(Q1s, Q2s, Truth).
'≻'(Q2s, Q1s, Truth) :-'≺'(Q1s, Q2s, Truth).

'≼'(Q1s, Q2s) :- '≼'(Q1s, Q2s, true).  # <2>
'⋠'(Q1s, Q2s) :- '≼'(Q1s, Q2s, false).
'≽'(Q2s, Q1s) :- '≼'(Q1s, Q2s, true).
'⋡'(Q2s, Q1s) :- '≼'(Q1s, Q2s, false).

'≺'(Q1s, Q2s) :- '≺'(Q1s, Q2s, true).
'⊀'(Q1s, Q2s) :- '≺'(Q1s, Q2s, false).
'≻'(Q2s, Q1s) :- '≺'(Q1s, Q2s, true).
'⊁'(Q2s, Q1s) :- '≺'(Q1s, Q2s, false).
```
1. The '≼' here is a *partially-instantiated* `('≼')/3`
2. `<op>/2`'s are defined in terms of `<op>/3`'s, not vice versa

---

### Aside

Does reification enlarge some subobject classifier $\Omega$?

``` {.tikz}
%%| width: 50%
%%| height: 50%
\begin{tikzcd}
    B \ar[d,tail,"m"'] \ar[r,"!"] & 1 \ar[d,"\top"]
    \\
    A \ar[r,"\chi(m)"] & \Omega
\end{tikzcd}
```

* Before: $\Omega$ = {`true`, `fail`}
* After: $\Omega$ = {`true`, `false`, `fail`}

---

> What can you do with it?
> It's like a lot of yaks jumping about!
> 
> --- Sir Thomas Beecham, on Beethoven's 7th Symphony


---

### Dose recommendations as a *functor*
$$
(Q^D,\preceq,\frac{0}{0},+) = \Q \hookleftarrow \Q_f \xrightarrow{F} \D = \{0 \le 1 \le ... \le D\}.
$$
``` {.prolog}
:- use_module(library(tabling)).
:- use_module(rcpearl). % Norris & Triska (2024) arXiv

:- table d_endtally_rec/3. % so DCG gets elaborated just once
d_endtally_rec(D, FinalTally, Rec) :-
    length(Init, D), maplist(=(0/0), Init), Init = [I|Is],
    phrase(path([I]-Is), Path),
    phrase((..., [Endstate,stop,recommend_dose(Rec)]), Path),
    state_tallies(Endstate, FinalTally).

%?- d_endtally_rec(2, Q, D).
%@    Q = [0/3,0/6], D = 2
%@ ;  Q = [0/3,1/6], D = 2
%@ ;  Q = [0/6,2/3], D = 1
%           . . .
%@ ;  Q = [4/6,0/0], D = 0
%@ ;  false. % 29 final tallies
```

## Checking *functoriality*

``` {.prolog code-line-numbers="1-4|1-6|1-11|"}
?- d_endtally_rec(2, Q1, D1),
   d_endtally_rec(2, Q2, D2),
      Q1 '≼' Q2,  % Q1 is evidently no safer than Q2,
   #\(D1 #=< D2). % yet D1 is NOT likewise related to D2.
%@    Q1 = [1/6,1/6], D1 = 2, Q2 = [0/6,2/6], D2 = 1
%@ ;  false.

?- d_endtally_rec(3, Q1, D1),
   d_endtally_rec(3, Q2, D2),
   Q1 '≼' Q2, % Q1 evidently no safer than Q2,
   D1 #>  D2. % yet recommended D1 exceeds D2.
%@    Q1 = [0/3,1/6,1/6], D1 = 3, Q2 = [0/3,0/6,2/6], D2 = 2
%@ ;  Q1 = [1/6,0/3,1/6], D1 = 3, Q2 = [0/3,0/6,2/6], D2 = 2
%@ ;  Q1 = [1/6,1/6,1/6], D1 = 3, Q2 = [1/6,0/6,2/6], D2 = 2
%@ ;  Q1 = [1/6,1/6,2/3], D1 = 2, Q2 = [0/6,2/6,2/3], D2 = 1
%@ ;  Q1 = [1/6,1/6,2/3], D1 = 2, Q2 = [0/6,2/6,2/6], D2 = 1
%@ ;  Q1 = [1/6,1/6,2/6], D1 = 2, Q2 = [0/6,2/6,2/6], D2 = 1
%@ ;  Q1 = [1/6,1/6,3/3], D1 = 2, Q2 = [0/6,2/6,0/0], D2 = 1
%@ ;  Q1 = [1/6,1/6,3/3], D1 = 2, Q2 = [0/6,2/6,2/3], D2 = 1
%@ ;  Q1 = [1/6,1/6,3/3], D1 = 2, Q2 = [0/6,2/6,2/6], D2 = 1
%@ ;  Q1 = [1/6,1/6,3/3], D1 = 2, Q2 = [0/6,2/6,3/3], D2 = 1
%@ ;  Q1 = [1/6,1/6,3/3], D1 = 2, Q2 = [0/6,2/6,3/6], D2 = 1
%@ ;  Q1 = [1/6,1/6,3/6], D1 = 2, Q2 = [0/6,2/6,2/6], D2 = 1
%@ ;  Q1 = [1/6,1/6,3/6], D1 = 2, Q2 = [0/6,2/6,3/6], D2 = 1
%@ ;  Q1 = [1/6,1/6,4/6], D1 = 2, Q2 = [0/6,2/6,2/6], D2 = 1
%@ ;  Q1 = [1/6,1/6,4/6], D1 = 2, Q2 = [0/6,2/6,3/6], D2 = 1
%@ ;  Q1 = [1/6,1/6,4/6], D1 = 2, Q2 = [0/6,2/6,4/6], D2 = 1
%@ ;  false. % 16 non-functorial pairs in D=3 trial!
```

## Hasse diagram for 2-dose 3+3 {.scrollable}

![](hasse2.svg){width=100%}

## 'Markov' or 'lack-of-memory' property

```{.verbatim code-line-numbers="1-3|5-7|"}
Level 2                 x o o  o o o  |  1/6  <-- MTD = RP2D
Level 1   o o x  o o o                |  1/6
Cohort#     1      2      3      4    | Tally = [1/6,1/6]
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Level 2          o x o  o o x         |  2/6  <-- Too toxic
Level 1   o o o                o o o  |  0/6  <-- MTD = RP2D
Cohort#     1      2      3      4    | Tally = [0/6,2/6]
```

::: {.fragment}
<cr>
Recall that
``` prolog
%?- [1/6,1/6]'≼'[0/6,2/6].
%@    true.
```
:::

::: {.incremental}
* In effect, the 3+3 'takes a Mulligan' on a 1st-cohort toxicity.
* Thus we obtain at least a *critical* method
:::


## What can we do *constructively*?

``` {.tikz}
%%| width: 100%
%%| height: 75%
\begin{tikzcd}[row sep=huge]
    & B \ar[ddr,"g"]
    \\ \\
    A \ar[uur,dashed,"f?"] \ar[rr,"h"'] & & C
    \\
    & ``\text{lifting problem}"
\end{tikzcd}
\qquad
\begin{tikzcd}[row sep=huge]
    & B \ar[ddr,dashed,"g?"]
    \\ \\
    A \ar[uur,"f"] \ar[rr,"h"'] & & C
    \\
    & ``\text{extension problem}"
\end{tikzcd}
```

::: aside
Lawvere & Schanuel (2009) *Conceptual Mathematics: A first introduction to categories*, p45
:::

---

### Extending $F$ along $\Q_f \hookrightarrow \Q$?

``` {.tikz}
%%| width: 100%
%%| height: 60%
\begin{tikzcd}[row sep=large]
\Q_f \ar[dr,hook,"\iota"'] \ar[rr,"F"{name=F},""{name=U,below}] & & \D \\
& \Q\, \ar[ur,dashed,"\text{Lan}_\iota F"']
\ar[Rightarrow,shorten=2.5mm,from=U,"\eta"]
\end{tikzcd}
\quad\text{or}\quad
\begin{tikzcd}[row sep=large]
\Q_f \ar[dr,hook,"\iota"'] \ar[rr,"F"{name=F},""{name=U,below}] & & \D \\
& \Q\, \ar[ur,dashed,"\text{Ran}_\iota F"']
\ar[Rightarrow,shorten=2.5mm,to=U,"\epsilon"']
\end{tikzcd}
```

---

### How about extending $\iota$ along $\Q_f \xrightarrow{F} \D$?

``` {.tikz}
%%| width: 100%
%%| height: 40%
  \begin{tikzcd}[row sep=large,column sep=small]
    \Q_f \ar[dr,"F"'] \ar[rr,hook,"\iota",""{name=U,below}] & & \Q \\
    & \D\, \ar[ur,dashed,"G=\text{Lan}_F \iota"']
      \ar[Rightarrow,shorten <=2mm,shorten >=4mm,from=U,"\eta"{pos=0.4}]
  \end{tikzcd}
  \begin{tikzcd}[row sep=large,column sep=small]
    \Q_f \ar[dr,"F"'] \ar[rr,hook,"\iota",""{name=U,below}] & & \Q \\
    & \D\, \ar[ur,"G'"']
      \ar[Rightarrow,shorten <=2mm,shorten >=4mm,from=U,"\gamma"{pos=0.4}]
  \end{tikzcd}
  =
  \begin{tikzcd}[row sep=large,column sep=small]
    \Q_f \ar[dr,"F"'] \ar[rr,hook,"\iota",""{name=U,below}] & & \Q \\
    & \D\, \ar[ur,bend left,"G"{pos=0.6,inner sep=0.2},""{name=H,below}]
           \ar[ur,bend right,"G'"'{inner sep=0},""{pos=0.35,name=I,above}]
           \ar[Rightarrow,from=H,to=I,"\exists !"{pos=0.4},"\alpha"'{pos=0.6}]
    \ar[Rightarrow,shorten <=2mm,shorten >=4mm,from=U,shift right=2mm,"\eta"'{pos=0.4}]
  \end{tikzcd}
```

The RHS 'pasting diagram' states a *universal mapping property* of $G = \text{Lan}_F\iota$, that any *other* $\D \xrightarrow{G'} \Q$ with natural transformation $\gamma : \iota \Rightarrow G'\circ F$ must have $\gamma$ factoring uniquely through $\eta$.

---

### Conditions on $G$

``` {.tikz}
%%| width: 100%
%%| height: 35%
  \begin{tikzcd}[row sep=large,column sep=small]
    \Q_f \ar[dr,"F"'] \ar[rr,hook,"\iota",""{name=U,below}] & & \Q \\
    & \D\, \ar[ur,dashed,"G=\text{Lan}_F \iota"']
      \ar[Rightarrow,shorten <=2mm,shorten >=4mm,from=U,"\eta"{pos=0.4}]
  \end{tikzcd}
  \begin{tikzcd}[row sep=large,column sep=small]
    \Q_f \ar[dr,"F"'] \ar[rr,hook,"\iota",""{name=U,below}] & & \Q \\
    & \D\, \ar[ur,"G'"']
      \ar[Rightarrow,shorten <=2mm,shorten >=4mm,from=U,"\gamma"{pos=0.4}]
  \end{tikzcd}
  =
  \begin{tikzcd}[row sep=large,column sep=small]
    \Q_f \ar[dr,"F"'] \ar[rr,hook,"\iota",""{name=U,below}] & & \Q \\
    & \D\, \ar[ur,bend left,"G"{pos=0.6,inner sep=0.2},""{name=H,below}]
           \ar[ur,bend right,"G'"'{inner sep=0},""{pos=0.35,name=I,above}]
           \ar[Rightarrow,from=H,to=I,"\exists !"{pos=0.4},"\alpha"'{pos=0.6}]
    \ar[Rightarrow,shorten <=2mm,shorten >=4mm,from=U,shift right=2mm,"\eta"'{pos=0.4}]
  \end{tikzcd}
```

Natural transformations $\eta, \gamma, \alpha$ are simply finite collections of arrows '$\preceq$' of $\Q$, indexed either by $\Q_f$ or (in $\alpha$'s case) by $\D$.  Thus the diagrams relate the images of $\Q_f$ under $\iota$ and $G F$:
$$
  \iota\Q_f \equiv \Q_f \preceq G F \Q_f \preceq G' F \Q_f.
$$

---

### Reframed in terms of *principal lower sets*

By serially restricting the foregoing to the nested sequence
$$
\Q_{f\downarrow d} := F^{-1}(\downarrow\!d) \subset \Q_f
$$
of preimages of the lower sets of $\D$, we obtain equivalently
$$
\Q_{f\downarrow d} \subseteq\; \downarrow\!G(d) \quad \forall d \in \D,
$$
the $G(d)$ being *minimal* among tallies for which this holds.  This pointwise statement can in turn can be translated
$$
  F(q) \le d \Longleftarrow q \preceq G(d).
$$

---

> Adjoint functors arise everywhere.
>
> --- Saunders Mac Lane

``` {.tikz}
%%| width: 80%
%%| height: 30%
\begin{tikzcd}[row sep=large,column sep=small]
\Q \ar[rrr,"E"{name=E,description}]
& & & \D
\ar[lll,bend right,out=-45,in=-135,"G"'{name=G}]
\arrow[phantom,from=G,to=E,"\dashv" rotate=90]
\end{tikzcd}
```

Perhaps the right adjoint to an *incremental enrollment* $E$ that is concordant with $F$:

$$
E(q) \le d \iff q \preceq G(d)
$$

## Finding minimal and maximal subsets

```prolog
qs_maxs([], []).
qs_maxs([Q|Qs], Maxs) :-
    tpartition('≽'(Q), Qs, _, Qs1),
    if_(tmember_t('≼'(Q), Qs1), % ∃ Q' ∈ Qs s.t. Q ≼ Q' ?
        qs_maxs(Qs1, Maxs),     % if so, Q is not maximal
        (   Maxs = [Q|Maxs1],   % otherwise, it is
            qs_maxs(Qs1, Maxs1)
        )).

qs_mins([], []).
qs_mins([Q|Qs], Mins) :-
    tpartition('≼'(Q), Qs, _, Qs1),
    if_(tmember_t('≽'(Q), Qs1), % ∃ Q' ∈ Qs s.t. Q' ≼ Q ?
        qs_mins(Qs1, Mins),     % if so, Q is not minimal
        (   Mins = [Q|Mins1],   % otherwise, it is
            qs_mins(Qs1, Mins1)
        )).
```

## Computing $G(\D) = \{g_1, ..., g_D\}$

```prolog
qs_d_nmax(Qs, D, Nmax) :-
    length(Qs, D),
    maplist(\Q^T^N^(Q = T/N), Qs, Ts, Ns),
    Ns ins 0..Nmax, label(Ns),
    maplist(\T^N^(T in 0..N), Ts, Ns), label(Ts).

d_gs_rec(D, Gs, X, Nmax) :-
    X in 0..D, indomain(X),
    % Calculate Qls = F⁻¹(0..X)
    findall(Qf, (d_endtally_rec(D, Qf, Xi), Xi in 0..X), Qls),
    qs_maxs(Qls, Qls1), % q ∈ Qls ⟹ ∃ q' ∈ Qls1 . q ≼ q'
    % Having calculated the maximal elements of F⁻¹(0..X),
    % we now proceed to search for all candidate gₓ's.
    findall(C, (qs_d_nmax(C, D, Nmax),
                maplist('≽'(C), Qls1)), Cs),
    qs_mins(Cs, Gs).

d_gs_rec(D, Gs, X) :- d_gs_rec(D, Gs, X, 6).
```

## Results for D in 2..3

```prolog
%?- time(d_gs_rec(2, Gs, X)).
%@    % CPU time: 2.526s, 11_246_152 inferences
%@    Gs = [[2/6,0/4]], X = 0
%@ ;  % CPU time: 0.934s, 4_083_756 inferences
%@    Gs = [[0/6,2/6]], X = 1
%@ ;  % CPU time: 0.918s, 3_999_748 inferences
%@    Gs = [[0/5,0/6]], X = 2.

%?- time(d_gs_rec(3, Gs, X)).
%@    % CPU time: 98.132s, 433_986_805 inferences
%@    Gs = [[2/6,0/6,0/2]], X = 0
%@ ;  % CPU time: 38.251s, 167_709_838 inferences
%@    Gs = [[0/6,2/6,0/4]], X = 1
%@ ;  % CPU time: 32.944s, 144_979_445 inferences
%@    Gs = [[0/5,0/6,2/6]], X = 2
%@ ;  % CPU time: 32.764s, 144_367_573 inferences
%@    Gs = [[0/5,0/5,0/6]], X = 3.

%?- time(d_gs_rec(4, Gs, X)).
%@    error('$interrupt_thrown',repl/0). % unbounded mem growth
```

## Computing *meets* (1)

```prolog
maxs(N1s, N2s, Ns) :- maplist(\N1^N2^N^(#N #= max(#N1, #N2)), N1s, N2s, Ns).
mins(N1s, N2s, Ns) :- maplist(\N1^N2^N^(#N #= min(#N1, #N2)), N1s, N2s, Ns).

% https://en.wikipedia.org/wiki/Monus#Natural_numbers
monus_(X, Y, X_Y) :- #X #>= 0, #Y #>= 0, #X_Y #= max(0, #X - #Y).
monus([X|Xs], [Y|Ys], [X_Y|Xs_Ys]) :-
    monus_(X, Y, X_Y),
    monus(Xs, Ys, Xs_Ys).
monus([], [], []).
    
%?- X=[5,7], Y=[8,9], monus(X, Y, X_Y), monus(Y, X, Y_X).
%@    X = [5,7], Y = [8,9], X_Y = [0,0], Y_X = [3,2].

all_but_last(Xs, Xs1, X) :-
    reverse(Xs, [X|Vs]),
    reverse(Vs, Xs1).

%?- all_but_last([1,2,3], B, L).
%@    B = [1,2], L = 3.
```

---

## Computing *meets* (2)

```prolog
% Find the maximal Ūs profile such that (Ts:Ūs) ≼ Qs.
qs_Ts_maxŪs(Qs, Ts, Ūs) :-
    qs_Ts_Ūs(Qs, Ts_, Ūs_), Ts_ '≤' Ts,
    monus(Ts, Ts_, As_), all_but_last(As_, As, _),
    same_length(Qs, Ūs),
    maplist(\U^U_^A^(#U #= #U_ + #A), Ūs, Ūs_, [0|As]).

meet(Q1s, Q2s, Qs) :-
    same_length(Q1s, Q2s), same_length(Q2s, Qs),
    qs_Ts_Ūs(Q1s, T1s, _), qs_Ts_Ūs(Q2s, T2s, _),
    maxs(T1s, T2s, Ts), % q = q₁ ∧ q₂ ⟹ Ts ≥ T1s ∨ T2s
    % Having set Ts to the bare-minimum T1s ∨ T2s compatible with
    % q = q₁ ∧ q₂, we now seek the highest compatible Ūs profile:
    qs_Ts_maxŪs(Q1s, Ts, Ū1s),
    qs_Ts_maxŪs(Q2s, Ts, Ū2s),
    mins(Ū1s, Ū2s, Ūs),
    qs_Ts_Ūs(Qs, Ts, Ūs).
```

Therefore $\Q$ is (at least) a lower semilattice.


## A naive first effort

```prolog
%?- time(d_gs(2, Gs)).
%@ Listing Qs......    % CPU time: 0.072s, 249_966 inferences
%@ Sorting length-784 list Qs:
%@   .. encoding Qs:   % CPU time: 0.636s, 3_382_043 inferences
%@    % CPU time: 0.640s, 3_386_627 inferences
%@ Stratifying Qf..    % CPU time: 0.769s, 3_442_312 inferences
%@ Finding g's ..
%@ ↓[2/6,0/4] ⊇ [[2/6,0/0],[2/6,2/6]].
%@ ↓[0/6,2/6] ⊇ [[0/6,2/6]].
%@ ↓[0/5,0/6] ⊇ [[0/3,0/6],[1/6,0/6]].
%@    % CPU time: 0.893s, 3_980_654 inferences
%@    % CPU time: 2.382s, 11_068_590 inferences
%@    Gs = [[2/6,0/4],[0/6,2/6],[0/5,0/6]].

e2(Q, X) :-
    [G0,G1,G2] = [[2/6,0/4], [0/6,2/6], [0/5,0/6]],
    if_(Q '≼' G0, X = 0,
        if_(Q '≼' G1, X = 1,
            if_(Q '≼' G2, X = 2, false))).

%?- e2([0/0,0/0], X).
%@    X = 2. % Is the bottom-up cascade of a lower-Galois IE therefore unsafe?
```

## Hints of a workable trial

```prolog
% Suppose we want a 'cut-off' version c2/2 of the above.
% The main deficiency of e2/2 to be remedied is that it
% lets Q 'slip thru' to the highest dose, simply because
% the po ≼ is too weak to catch it.  What we would need
% then is to impose _additional_ requirements on upward
% percolation of Q.
c2(Q, X) :-
    [G0,G1,G2] = [[2/6,0/4], [0/6,2/6], [0/5,0/6]],
    if_(Q '≼' G0,
        X = 0,
        if_(( Q '⋡' G0
            ; Q '≼' G1
            ), X = 1,
            if_(( Q '⋡' G1
                ; Q '≼' G2
                ), X = 2,
                false))).

%?- c2([0/0,0/0], X).
%@    X = 1.

%?- c2([1/1,0/0], X).
%@    X = 1.

%?- c2([2/2,0/0], X).
%@    X = 0.
```

## Pending work {.scrollable}

* Obtain workable 3+3-concordant incremental enrollments

* More direct [yet still performant] CLP(ℤ) implementation
  - $\preceq$
  - covering relation via `library(clpb)`?
  
* Liberate predicates to work in several directions
  - including with *fair enumeration* of $Q^D$

* Exhaustive searches for counterexamples to Theorems

* Apply to larger classes of dose-finding designs (BOIN, CRM)

* Formalize the mathematics in Agda or Shen

# Thank you!

https://github.com/Precisfice/DEDUCTION/


# Backup slides

---

### Fast calculation of $G$

```prolog
'⋡'(Q1s, Q2s, Truth) :- '≽'(Q1s, Q2s, Untruth), reif:non(Untruth, Truth).

% Search [Q|Qs] for first Gx satisfying ↓Gx ⊇ Ms, i.e. Q ≼ Gx ∀ Q ∈ Ms.
galois([Ms|Mss], [Q|Qs], [G|Gs]) :-
    if_(tmember_t('⋡'(Q), Ms),        % ∃ M ∈ Ms s.t. M ⋠ Q ?
        galois([Ms|Mss], Qs, [G|Gs]), % if so, Q is not a Gx;
        (   format("↓~w ⊇ ~w.~n", [Q, Ms]),
            G = Q,                    % otherwise, collect it
            galois(Mss, Qs, Gs)       % and recurse.
        )).
galois([], _, []). % Succeed when all strata are accounted-for.

d_gs(D, Gs) :-
    time(findall(Q, qs_d_nmax(Q, D, 6), Qs)),
    qs_sorted(Qs, SQs),                        # <1>
    reverse(SQs, RQs),
    time(d_Qfstratamax(D, Mss)),
    time(galois(Mss, RQs, Gs)).
```
1. Involves encoding $\Q \hookrightarrow (\mathbb{N},\le)$!

---

### Embedding $\Q \hookrightarrow (\mathbb{N},\le)$

```prolog
int_bases_digits(K, [B|Bs], [W|Ws]) :-
    same_length(Bs, Ws),
    #W #= #K mod #B,
    #K1 #= #K div #B,
    int_bases_digits(K1, Bs, Ws).

int_bases_digits(_, [], []).

%?- int_bases_digits(1023, [2,2,2,2,2,2,2,2,2,2], Ds).
%@    Ds = [1,1,1,1,1,1,1,1,1,1].

%?- int_bases_digits(1023, [16,16,2,2], Ds).
%@    Ds = [15,15,1,1].

% The length-D list of bases begins 6 + 1 = 7, and adds 6 at each level.
d_bases(D, [B|Bs]) :-
    length([B|Bs], D),
    same_length(Xs, Bs),
    maplist(=(6), Xs),
    intlist_partsums([7|Xs], [B|Bs]).

d_int_ws(D, K, Ws) :-
    d_bases(D, Bs),
    int_bases_digits(K, Bs, Ws).

pvs([P|Ps]) :-
    pvs(Ps),
    pvs_nextup(Ps, P).
pvs([]).

pvs_nextup([], 1).
pvs_nextup([P|Ps], P1) :-
    length([P|Ps], N),
    #P1 #= #P * (6*N + 1).

% Let's just PRECOMPUTE!
%?- length(Ps, 8), pvs(Ps), reverse(Ps, Rs).
%@    Ps = [2131900225,49579075,1339975,43225,1729,91,7,1],
%     Rs = [1,7,91,1729,43225,1339975,49579075,2131900225].

placevalues(Ps) :-
    same_length(Ps, Rs),
    % NB: Taking a _tail_ with append/3 would leave a choice point.
    append(Rs, _, [1,7,91,1729,43225,1339975,49579075,2131900225]),
    reverse(Rs, Ps).

%?- length(Ps, 5), placevalues(Ps).
%@    Ps = [43225,1729,91,7,1].

ws_int(Ws, K) :-
    same_length(Ws, Ps),
    placevalues(Ps),
    reverse(Ws, RWs), % our Us and Ts are typically indexed 1..D
    scalar_product(RWs, Ps, #=, #K).

%% d_maxenc(D, Kmax) :-
%%     length(Ps, D),
%%     placevalues([Kmax1|Ps]),
%%     #Kmax #= Kmax1 - 1.

%?- d_maxenc(5, Kmax).
%@    Kmax = 1339974.

% I've already precomputed placevalues/1, but might I still
% gain additional speedup by precomputing d_maxenc/2 also?
%?- D in 0..7, indomain(D), d_maxenc(D, Kmax).
%@    D = 0, Kmax = 0
%@ ;  D = 1, Kmax = 6
%@ ;  D = 2, Kmax = 90
%@ ;  D = 3, Kmax = 1728
%@ ;  D = 4, Kmax = 43224
%@ ;  D = 5, Kmax = 1339974
%@ ;  D = 6, Kmax = 49579074
%@ ;  D = 7, Kmax = 2131900224.

d_maxenc(1, 6).
d_maxenc(2, 90).
d_maxenc(3, 1728).
d_maxenc(4, 43224).
d_maxenc(5, 1339974).
d_maxenc(6, 49579074).
d_maxenc(7, 2131900224).

qs_int(Qs, K) :-
    qs_Ts_Ūs(Qs, Ts, Ūs),
    ws_int(Ts, KT),
    ws_int(Ūs, KŪ),
    length(Qs, D), d_maxenc(D, Kmax),
    #K #= (#Kmax + 1) * #KT + (#Kmax - #KŪ).
```

---

### Computing the *covering* relation

```prolog
% NB: The time-complexity of in_cover_t/3 could be reduced
%     by exploiting the arithmetized sort behind d_strata/2.
%     But we retain this implementation for the time being,
%     since its simplicity renders it 'obviously' correct.
in_cover_t(Qs, Q1, Q2, Truth) :-
    member(Q1, Qs), member(Q2, Qs), Q1 '≺' Q2,
    if_(tmember_t(between_t(Q1,Q2), Qs),
        Truth = false,
        Truth = true
       ).

between_t(Q1, Q2, Q, Truth) :-
    if_((Q1 '≺' Q, Q '≺' Q2),
        Truth = true,
        Truth = false
       ).

in_cover(Qs, Q1, Q2) :- in_cover_t(Qs, Q1, Q2, true).

d_ncovers(D, N) :-
    findall(Q, d_mendtally_rec(D,Q,_), Qs),
    findall(Q1-Q2, in_cover(Qs, Q1, Q2), Covers),
    length(Covers, N).

%?- time(d_ncovers(2, N)).
%@    % CPU time: 8.045s, 40_522_204 inferences
%@    N = 50.

%?- time(d_ncovers(3, N)).
%@    % CPU time: 242.710s, 1_230_061_670 inferences
%@    N = 194.
```
